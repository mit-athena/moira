#!/moira/bin/perl -Tw
# $Id$

$ENV{'PATH'} = "";

$MR_ARGV_LEN = 19;
$ADDRESS_POS = 10;
$OPT_POS = 16;

@URLS = ('https://hostsec-rs-1.mit.edu/cgi-bin/route.pl', 'https://hostsec-rs-2.mit.edu/cgi-bin/route.pl');
$STOP_FILE = "/moira/route-server/noroute";
$CRED_FILE = "/moira/route-server/creds";

use JSON;
use LWP;
use File::Basename;

my @input = @ARGV;

$whoami = basename($0);

print "$whoami @ARGV\n";

if (!@ARGV) {
  critical_alert("$whoami: no moira input received via ARGV");
}

# Support a stop file.
$i = 0;
while (-e $STOP_FILE) {
    sleep 60;
    if ($i > 30) {
	critical_alert("$whoami: incremental failed ($STOP_FILE exists");
    }
    $i++;
}

my $type       = shift(@input);
my $before_len = shift(@input);
my $after_len  = shift(@input);

if ($type eq "machine") {
    if ($before_len == 0 && $after_len == $MR_ARGV_LEN) {
	# machine add, do nothing.
	exit 0;
    } elsif ($before_len == $MR_ARGV_LEN && $after_len == $MR_ARGV_LEN) {
	do_machine(@input);
	exit 0;
    } elsif ($before_len == $MR_ARGV_LEN && $after_len == 0) {
	do_machine(@input);
	exit 0;
    } else {
	critical_alert("$whoami: malformed $type incremental arguments");
    }
} else {
    critical_alert("$whoami: called on unexpected table $type");
}

exit 0;

sub critical_alert {
    my ($error) = @_;

    my $logfile = "/moira/critical.log";
    open(LOG, ">>$logfile");
    $tnow = localtime;
    print LOG "$tnow <$$> $error\n";
    close(LOG);

    $error =~ /(.*)/;
    $error = $1;

    my $zwrite = "/usr/local/bin/zwrite";

    if (-f $zwrite) {
	system($zwrite, "-q", "-d", "-n", "-c", "moira", "-i", "incremental", "-m", "$error");
    }
    
    exit 1;
}

sub do_machine {

    my (@moira_input) = @_;

    $before_address = $moira_input[$ADDRESS_POS];

    if ($after_len == 0) {
	# machine deletion
	if ($before_address ne "unassigned") {
	    update_route_server("none", $before_address);
	}
    } else {

	$after_address = $moira_input[$after_len + $ADDRESS_POS];
	$after_option = $moira_input[$after_len + $OPT_POS];

	if ($after_option == 0) {
	    $after_state = "none";
	} elsif ($after_option == 1 || $after_option == 2) {
	    $after_state = "optout";
	} elsif ($after_option == 3) {
	    $after_state = "blackhole";
	} else {
	    critical_alert("$whoami: Unexpected machine opt $after_option");
	}

	# Did our address change?
	if ($before_address ne $after_address) {
	    # If it did, consider before and after states independently.

	    if ($before_address ne "unassigned") {
		# Delete the address that's no longer in use.
		update_route_server("none", $before_address);
	    }

	    if ($after_address ne "unassigned") {
		    update_route_server($after_state, $after_address);
	    }
	} else {
	    if ($after_address ne "unassigned") {
		update_route_server($after_state, $after_address);
	    }
	}
    }

    return;
}

sub update_route_server {

    my ($state, $address) = @_;
    my %outhash = ();

    $outhash{'state'} = $state;
    $outhash{'ip'} = $address;

    my $json = encode_json(\%outhash);

    open(CREDS, $CRED_FILE) or critical_alert("$whoami: Unable to open credentials file");
    while (<CREDS>) {
        ($username, $password) = split(/:/);
        last;
    }

    foreach $URL (@URLS) {
	print "POSTing to $URL: $state $prevstate $address\n";

	my $req = HTTP::Request->new('POST', $URL);
	$req->header( 'Content-Type' => 'application/json' );
	$req->authorization_basic($username, $password);
	$req->content( $json );

	my $lwp = LWP::UserAgent->new;
	my $res = $lwp->request($req);
	
	if (!$res->is_success) {
	    print "failed POSTing to $URL: " . $res->decoded_content;
	    push(@errors, " failed POSTing to $URL");
	} else {
	    print "$URL response: " . $res->decoded_content;
	}
    }

    if (scalar(@errors) > 0) {
	foreach $error (@errors) {
	    $allerrors .= $error;
	}
	critical_alert("$whoami:$allerrors");
    }

    return;
}
